buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.7.1'
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.3"
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'com.github.johnrengelman.shadow'


sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs.addAll(['--release', '8'])
}

ext {
    roboVMVersion = '2.3.12.1'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    compileOnly gradleApi()
    compileOnly localGroovy()
    compile "org.apache.commons:commons-compress:1.5"
    compile "com.mobidevelop.robovm:robovm-dist-compiler:${roboVMVersion}"
    compile 'org.sonatype.aether:aether:1.13.1'
    compile 'org.sonatype.aether:aether-connector-wagon:1.13.1'
    compile 'org.apache.maven:maven-aether-provider:3.0.4'
    compile 'org.apache.maven.wagon:wagon-provider-api:2.4'
    compile 'org.apache.maven.wagon:wagon-http:2.4'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.mobidevelop.robovm'
            artifactId = 'robovm-gradle-plugin'
            version = '2.3.12.1'
            from components.java

            pom {
                name = 'RoboVM Gradle Plugin'
                packaging = 'jar'
                description = 'The RoboVM Gradle Plugin provides a way to build RoboVM apps using Gradle.'
                url = 'https://github.com/mobidevelop/robovm/'
                inceptionYear = '2013'

                scm {
                    url = 'https://github.com/mobidevelop/robovm.git'
                    connection = 'scm:git:git://github.com/mobidevelop/robovm.git'
                    developerConnection = 'scm:git:git@github.com:mobidevelop/robovm.git'
                }

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'jtakakura'
                        name = 'Junji Takakura'
                    }
                    developer {
                        id = 'ntherning'
                        name = 'Niklas Therning'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            // TODO: FIXME: provide proper release/snapshot repositories
            url = 'https://repo.repsy.io/mvn/dkimitsa/alt'
            credentials {
                username = 'dkimitsa'
                password = ''
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.10.1'
}

shadowJar {
    classifier = ''
    dependencies {
        exclude(dependency("com.mobidevelop.robovm:robovm-dist-compiler:${roboVMVersion}"));
    }
    relocate 'org.apache.http', 'com.mobidevelop.robovm.org.apache.http'
    relocate 'org.apache.commons.io', 'com.mobidevelop.robovm.org.apache.commons.io'
    relocate 'org.objectweb.asm', 'com.mobidevelop.robovm.asm'
}

assemble.dependsOn('shadowJar')
publish.dependsOn('shadowJar')
publishToMavenLocal.dependsOn('shadowJar')
